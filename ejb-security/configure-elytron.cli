# Batch script to enable elytron for the quickstart application in the JBoss EAP server

# Start batching commands
batch

# Add filesystem realm that resides in directory fs-realm-users inside the base configuration directory defined by jboss.server.config.dir property.
/subsystem=elytron/filesystem-realm=fsRealm:add(path=fs-realm-users, relative-to=jboss.server.config.dir)

# Add the quickstart identity
/subsystem=elytron/filesystem-realm=fsRealm:add-identity(identity=quickstartUser)

# Add the quickstart password
/subsystem=elytron/filesystem-realm=fsRealm:set-password(clear={password=quickstartPwd1!},identity=quickstartUser)

# Add the role for quickstart identity
/subsystem=elytron/filesystem-realm=fsRealm:add-identity-attribute(identity=quickstartUser, name=Roles, value=[guest])

# Add a simple-role-decoder
/subsystem=elytron/simple-role-decoder=from-roles-attribute:add(attribute=Roles)

# Configure the security domain using the filesystem realm
/subsystem=elytron/security-domain=fsSD:add(realms=[{realm=fsRealm, role-decoder=from-roles-attribute}], default-realm=fsRealm,permission-mapper=default-permission-mapper)

# Add the filesystem security domain mapping in the EJB3 subsystem
/subsystem=ejb3/application-security-domain=other:add(security-domain=fsSD)

# Update the sasl-authentication-factory to use the fsSD security domain
/subsystem=elytron/sasl-authentication-factory=application-sasl-authentication:write-attribute(name=security-domain, value=fsSD)

# Update the sasl-authentication-factory to use SCRAM-SHA-512
/subsystem=elytron/sasl-authentication-factory=application-sasl-authentication:list-add(name=mechanism-configurations, value={mechanism-name=SCRAM-SHA-512})

# Update the http-remoting-connector to use the application-sasl-authentication factory
/subsystem=remoting/http-connector=http-remoting-connector:write-attribute(name=sasl-authentication-factory,value=application-sasl-authentication)

# Run the batch commands
run-batch

# Reload the server configuration
reload


