# Batch script to enable Elytron for the quickstart application in the JBoss EAP server

# Start batching commands
batch

# Configure a filesystem realm with a single identity
/subsystem=elytron/filesystem-realm=fsRealm:add(path=fs-users, relative-to=jboss.server.config.dir, hash-encoding=hex, hash-charset=KOI8-R)
/subsystem=elytron/filesystem-realm=fsRealm:add-identity(identity=quickstartUser)
/subsystem=elytron/filesystem-realm=fsRealm:set-password(digest={algorithm=digest-md5, realm=fsRealm, password=пароль}, identity=quickstartUser)

/subsystem=elytron/filesystem-realm=fsRealm:add-identity-attribute(identity=quickstartUser, name=Roles, value=["Admin", "Guest"])

# Add a simple role decoder
/subsystem=elytron/simple-role-decoder=from-roles-attribute:add(attribute=Roles)

# Configure a new security domain to use our filesystem realm and role decoder
/subsystem=elytron/security-domain=fsDomain:add(realms=[{realm=fsRealm, role-decoder=from-roles-attribute}], \
default-realm=fsRealm,permission-mapper=default-permission-mapper)

# Add the case security domain mapping in the EJB3 subsystem
/subsystem=ejb3/application-security-domain=other:add(security-domain=fsDomain)

# Update the sasl-authentication-factory to use the fsDomain
/subsystem=elytron/sasl-authentication-factory=application-sasl-authentication:write-attribute(name=security-domain, value=fsDomain)

# Update the sasl-authentication-factory to use the PLAIN mechanism
/subsystem=elytron/sasl-authentication-factory=application-sasl-authentication:list-add(name=mechanism-configurations, value={mechanism-name=PLAIN})

# Update the http-remoting-connector to use the application-sasl-authentication factory
/subsystem=remoting/http-connector=http-remoting-connector:write-attribute(name=sasl-authentication-factory,value=application-sasl-authentication)

# Run the batch commands
run-batch

# Reload the server configuration
reload
